plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'net.volcanomobile'
version '0.3.1'

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "loki666"
            name "Philippe Simons"
            email "contact@volcanomobile.net"
        }
    }

    scm {
        url "https://github.com/VolcanoMobile/euclidean-sequence-generator"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'net.volcanomobile'
            artifactId 'euclidean-sequence-generator'
            version '0.3.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Eucliedean Sequence Generator')
                root.appendNode('description', 'Simple Euclidean Sequencer Generator implementation.')
                root.children().last() + pomConfig
            }
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

Properties properties = new Properties()
File file = rootProject.file('local.properties')
if (file.exists()) {
    properties.load(file.newDataInputStream())
}

bintray {
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    publications = ['MyPublication']
    pkg {
        userOrg = 'volcanomobile'
        repo = 'public'
        name = 'euclidean-sequence-generator'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/VolcanoMobile/euclidean-sequence-generator.git'
        githubRepo = 'VolcanoMobile/euclidean-sequence-generator'
    }
}